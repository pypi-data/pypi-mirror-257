start : (sentence | sentence_tone)+

// Two options for dealing with potentially ambiguous syllable parsing, however mixing the two is dangerous...

// 1. all syllables in a word must be separated either by tone number or punctuation
sentence : ( PUNCTUATION | SPACE )* word_sep ( ( PUNCTUATION | SPACE )+ word_sep )* ( PUNCTUATION | SPACE )*
word_sep : ( syllable_tone | syllable_toneless ) ( SYLLABLE_SEP ( syllable_tone | syllable_toneless) )*

// 2. all syllables must have tone number (including 0), so no ambiguities about syllable separation
sentence_tone : ( PUNCTUATION | SPACE )* word_tone ( ( PUNCTUATION | SPACE )+ word_tone )* ( PUNCTUATION | SPACE )*
word_tone : syllable_tone+

// Syllables
// syllable : [initial] final tone?
syllable_tone : [initial] ( ( final tone ) | ( final_entering tone_entering ) )
syllable_toneless : [initial] ( final | final_entering ) 

// Initials
initial : INIT_BH | INIT_P  | INIT_B
        | INIT_M  | INIT_NG | INIT_N
        | INIT_GH | INIT_K  | INIT_G
        | INIT_D  | INIT_T 
        | INIT_Z  | INIT_C 
        | INIT_S  | INIT_H 
        | INIT_R  | INIT_L 

// Finals
final : ( medial ( codanasal | NASAL )? ) | codanasal
final_entering : medial ( NASAL? codastop )

// Medials
// longer medials are listed first to be preferentially matched
medial : MED_AI  | MED_AU  
       | MED_IA  | MED_IAU | MED_IEU | MED_IOU | MED_IU  | MED_IE  | MED_IO  
       | MED_OI  | MED_OU  
       | MED_UAI | MED_UA  | MED_UE  | MED_UI  
       | MED_A   | MED_V   | MED_E   | MED_I   | MED_O   | MED_U   

// Codas
coda : codanasal | codastop
codanasal : COD_M | COD_NG
codastop : COD_P | COD_K | COD_H

// Tones
tone : tone_citation ( "(" tone_changed ")" )?
tone_citation: tone_1 | tone_2 | tone_3 | tone_5 | tone_6 | tone_7
tone_changed: tone_1 | tone_2 | tone_5 | tone_6 | tone_7
tone_entering : ( tone_4 ( "(" tone_8 ")" )? ) | ( tone_8 ( "(" tone_4 ")" )? )

// Syllable separator
// hyphen or apostrophe are also valid punctuation
// usage as syllable separator has priority over use as punctuation
SYLLABLE_SEP.1 : "-" | "'" | "’"

// Punctuation and spacing
// syllable separator has priority over other punctuation
PUNCTUATION.0 : "." | "," | ":" | ";" | "?" | "!" | "'" | "-" | "(" | ")" | "[" | "]" | "“" | "”" | "‘" | "’"
SPACE : " "
