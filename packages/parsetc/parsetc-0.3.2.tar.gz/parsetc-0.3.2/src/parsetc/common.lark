start : (sentence | sentence_tone)+

// Two options for dealing with potentially ambiguous syllable parsing, however mixing the two is dangerous...

// 1. all syllables in a word must be separated either by tone number or punctuation
sentence : ( PUNCTUATION | SPACE )* word_sep ( ( PUNCTUATION | SPACE )+ word_sep )* ( PUNCTUATION | SPACE )*
word_sep : ( syllable_tone | syllable_toneless ) ( SYLLABLE_SEP ( syllable_tone | syllable_toneless) )*

// 2. all syllables must have tone number (including 0), so no ambiguities about syllable separation
sentence_tone : ( PUNCTUATION | SPACE )* word_tone ( ( PUNCTUATION | SPACE )+ word_tone )* ( PUNCTUATION | SPACE )*
word_tone : syllable_tone+

// Syllables
// syllable : initial? final tone?
syllable_tone : initial? final tone
syllable_toneless : initial? final

// Initials
// TODO: null initial
initial : INIT_BH | INIT_P  | INIT_B
        | INIT_M  | INIT_NG | INIT_N
        | INIT_GH | INIT_K  | INIT_G
        | INIT_D  | INIT_T 
        | INIT_Z  | INIT_C 
        | INIT_S  | INIT_H 
        | INIT_R  | INIT_L 

// Finals
// TODO: rule for entering tone
final : ( medial ( coda | ( NASAL codastops? ) )? ) | codanasal

// Medials
// longer medials are listed first to be preferentially matched
medial : MED_AI  | MED_AU  
       | MED_IA  | MED_IAU | MED_IEU | MED_IOU | MED_IU  | MED_IE  | MED_IO  
       | MED_OI  | MED_OU  
       | MED_UAI | MED_UA  | MED_UE  | MED_UI  
       | MED_A   | MED_V   | MED_E   | MED_I   | MED_O   | MED_U   

// Codas
coda : codanasal | codastops
codanasal : COD_M | COD_NG
codastops : COD_P | COD_K | COD_H

// Tones
tone : TONENUMBER ( "(" TONENUMBER ")" )?
TONENUMBER : "0".."8"

// Syllable separator
// hyphen or apostrophe are also valid punctuation
// usage as syllable separator has priority over use as punctuation
SYLLABLE_SEP.1 : "-" | "'" | "’"

// Punctuation and spacing
// syllable separator has priority over other punctuation
PUNCTUATION.0 : "." | "," | ":" | ";" | "?" | "!" | "'" | "-" | "(" | ")" | "[" | "]" | "“" | "”" | "‘" | "’"
SPACE : " "
